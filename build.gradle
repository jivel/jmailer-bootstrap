ext {
  springVersion = '4.1.7.RELEASE'
  aspectjVersion = '1.8.7'
  groovyVersion = '2.4.5'
  jmsApiVersion = '1.1-rev-1'
  javassistVersion = '3.8.0.GA'
  hibernateValidatorVersion = '5.1.3.Final'
  currentEnvironment = project.hasProperty("environment")?environment:"development"
  jmailerConfigurationDir = "${System.getProperty('user.home')}/.jmailer"
}

buildscript {
  repositories { jcenter() }
}

subprojects {
  apply plugin:'groovy'
  version = "0.0.1"
  group = "com.jos.dem"
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

project(":emailer:sender"){
  dependencies{
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile 'org.springframework.integration:spring-integration-mail:4.2.0.RELEASE'
    compile "org.freemarker:freemarker:2.3.23"
    compile 'javax.mail:mail:1.4.7'
  }
}

project(":emailer:formatter"){
  dependencies{
    compile "javax.jms:jms-api:$jmsApiVersion"
    compile "org.hibernate:hibernate-validator:$hibernateValidatorVersion"
    compile 'org.apache.activemq:activemq-spring:5.12.0'
    compile project(":emailer:sender")
  }
}

project(":web"){
  apply plugin: 'groovy'
  apply plugin: 'war'
  apply plugin: 'jetty'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {

    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-jms:$springVersion"
    compile "org.aspectj:aspectjrt:$aspectjVersion"
    compile "org.aspectj:aspectjweaver:$aspectjVersion"
    compile 'com.google.code.gson:gson:2.2.2'
    compile 'log4j:log4j:1.2.17'
    compile 'javax.servlet:servlet-api:2.5'
    compile project(":emailer:formatter")

  }

  jettyRun{
    contextPath = "jmailer"
    httpPort = 8080
  }

  jettyRunWar{
    contextPath = "jmailer"
    httpPort = 8080
  }

  println "Setting environment to: ${currentEnvironment}"

  def filesToInclude = [
  "*-${currentEnvironment}.properties"
  ]

  def toDirectory = 'src/main/resources/config'
  task settingEnvironment(type:Copy) {
    from jmailerConfigurationDir
    into toDirectory
    include filesToInclude
    rename { String fileName -> fileName.replace("-${currentEnvironment}", '') }
  }

  task settingLog4jProperties(type:Copy){
    from "${System.getProperty('user.home')}/.jmailer/log4j-${currentEnvironment}.properties"
    into "src/main/resources/"
    rename { String fileName -> fileName.replace("-${currentEnvironment}", '') }
  }

  processResources.dependsOn "settingEnvironment","settingLog4jProperties"
}
